plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains:annotations:24.1.0"

    api "org.joml:joml:1.10.8"
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = "https://maven.lenni0451.net/" + (project.maven_version.endsWith("SNAPSHOT") ? "snapshots" : "releases")

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "SoftwareRenderer"
                description = "Library for rendering simple 2D and 3D scenes in pure Java"
                url = "https://github.com/RaphiMC/SoftwareRenderer"
                licenses {
                    license {
                        name = "LGPL-3.0 License"
                        url = "https://github.com/RaphiMC/SoftwareRenderer/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/RaphiMC/SoftwareRenderer.git"
                    developerConnection = "scm:git:ssh://github.com/RaphiMC/SoftwareRenderer.git"
                    url = "https://github.com/RaphiMC/SoftwareRenderer.git"
                }
            }
        }
    }
}

signing {
    setRequired(false)
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
